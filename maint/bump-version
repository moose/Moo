#!/usr/bin/env perl

use strict;
use warnings FATAL => 'all';
use autodie;

chomp(my $LATEST = qx(grep '^[0-9]' Changes | head -1 | awk '{print \$1}'));

my @parts = split /\./, $LATEST;
if (@parts == 2) {
  @parts[1,2] = $parts[1] =~ /(\d{1,3})(\d{1,3})/;
}

my $OLD_DECIMAL = sprintf('%i.%03i%03i', @parts);

my %bump_part = (major => 0, minor => 1, bugfix => 2);

my $bump_this = $bump_part{$ARGV[0]||'bugfix'};

die "no idea which part to bump - $ARGV[0] means nothing to me"
  unless defined($bump_this);

my @new_parts = @parts;

$new_parts[$bump_this]++;
$new_parts[$_] = 0 for ($bump_this+1 .. 2);

my $NEW_DECIMAL = sprintf('%i.%03i%03i', @new_parts);

warn "Bumping $OLD_DECIMAL -> $NEW_DECIMAL\n";

for my $PM_FILE (qw(
  lib/Moo.pm
  lib/Moo/Role.pm
  lib/Sub/Defer.pm
  lib/Sub/Quote.pm
)) {
  my $file = do { local (@ARGV, $/) = ($PM_FILE); <> };

  $file =~ s/(?<=\$VERSION = ')${\quotemeta $OLD_DECIMAL}/${NEW_DECIMAL}/
    or die "unable to bump version number in $PM_FILE";

  open my $out, '>', $PM_FILE;

  print $out $file;
}
