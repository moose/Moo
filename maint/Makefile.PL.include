BEGIN {
  if (!-e 'META.yml') {
    -e 'Distar' or system qw(git clone https://github.com/p5sagit/Distar.git) and die;
    unshift @INC, 'Distar/lib';
    require Distar;
    Distar->import;
  }
  else {
    *manifest_include = sub {};
  }
}

sub WriteMakefileFromMeta {
  my ($META, $MM_ARGS) = @_;
  my %META = %$META;
  my %MM_ARGS = %{$MM_ARGS || {}};
  require ExtUtils::MakeMaker;

  my $eumm_version  = $ExtUtils::MakeMaker::VERSION;
  # have to do this since old EUMM dev releases include underscores
  $eumm_version =~ tr/_//d;
  my $mymeta        = $eumm_version >= 6.57_02;
  my $mymeta_broken = $mymeta && $eumm_version < 6.57_07;

  ($MM_ARGS{NAME} = $META{name}) =~ s/-/::/g;

  $META{author} = [ $META{author} ]
    if $META{author} && !ref $META{author};

  if (my $author = $MM_ARGS{AUTHOR} || $META{author}) {
    $author = ref $author ? $author : [ $author ];
    $MM_ARGS{AUTHOR} = $eumm_version >= 6.5702 ? $author : join(', ', @$author);
  }

  ($MM_ARGS{VERSION_FROM} = "lib/$MM_ARGS{NAME}.pm") =~ s{::}{/}g;
  $META{license} = [ $META{license} ]
    if $META{license} && !ref $META{license};
  $MM_ARGS{LICENSE} ||= $META{license}[0]
    if $META{license} && $eumm_version >= 6.30;
  $MM_ARGS{NO_MYMETA} = 1
    if $mymeta_broken;
  $MM_ARGS{META_ADD} = { 'meta-spec' => { version => 2 }, %META }
    unless -f 'META.yml';
  $MM_ARGS{PL_FILES} ||= {};
  $MM_ARGS{NORECURS} = 1
    if not exists $MM_ARGS{NORECURS};

  for (qw(configure build test runtime)) {
    my $key = $_ eq 'runtime' ? 'PREREQ_PM' : uc $_.'_REQUIRES';
    my $r = $MM_ARGS{$key} = {
      %{$META{prereqs}{$_}{requires} || {}},
      %{delete $MM_ARGS{$key} || {}},
    };
    defined $r->{$_} or delete $r->{$_} for keys %$r;
  }

  $MM_ARGS{MIN_PERL_VERSION} = delete $MM_ARGS{PREREQ_PM}{perl} || 0;

  delete $MM_ARGS{MIN_PERL_VERSION}
    if $eumm_version < 6.47_01;
  $MM_ARGS{BUILD_REQUIRES} = {%{$MM_ARGS{BUILD_REQUIRES}}, %{delete $MM_ARGS{TEST_REQUIRES}}}
    if $eumm_version < 6.63_03;
  $MM_ARGS{PREREQ_PM} = {%{$MM_ARGS{PREREQ_PM}}, %{delete $MM_ARGS{BUILD_REQUIRES}}}
    if $eumm_version < 6.55_01;
  delete $MM_ARGS{CONFIGURE_REQUIRES}
    if $eumm_version < 6.51_03;

  ExtUtils::MakeMaker::WriteMakefile(%MM_ARGS);
}

1;
